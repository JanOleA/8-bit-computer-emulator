pressing = 255            ; memory location for storing whatever number the user pressed last
multiplicand = 254        ; multiplicand for the multiply routine
multiplier = 253          ; multiplier in the multiply routine (how many times .multiplicand is added up)
product = 252             ; product of the multiplication, or quotient for the division
result = 251              ; result of any calculation
userval = 250             ; the entire inputted number (i.e., if the user presses "2" and then "3", it will be "23")
remainder = 249           ; remainder after a division
pow2 = 248
quotient = 247            ;
dividend = 246            ; dividend for divide routine
divisor = 245             ; divisor for divide routine

start:
  LDI 0             ; load 0 to clear required variables
  STA .pressing
  STA .result
  STA .userval
check_input:
  KEI               ; load value from input register
  ADI 128           ; add 128 to the value
  JPC input         ; if result > 255, jump to input handling
  JMP check_input   ; otherwise, continue check_input loop
input:
  ADI 192           ; add 192 to the value
  JPC handle_add_div; if carry, add or div button was pressed
  ADI 32            ; if not, add 32 to the value
  JPC handle_sub    ; if carry, sub button was pressed
  ADI 16            ; if not, add 16 to the value
  JPC handle_mul    ; if carry, mul button was pressed
  ADI 16            ; if not, add 16 to clear top 4 bits

  STA .pressing
  LDI 10            ; the current input value is stored in 250, must multiply by ten to insert new number at end
  STA .multiplier
  LDA .userval      ; load the current userval to multiply it by 10
  STA .multiplicand
  JSR multiply      ; multiply .multiplicand with .multiplier
  ADD .pressing     ; add the currently pressed number to the result of the multiplication
  OUT               ; display the value
  STA .userval      ; store the result to .userval

chk_no_inpt:        ; loop for checking whether no input is pressed
  KEI               ; load value from input register
  ADI 0             ; add zero to it
  JPZ check_input   ; jump to check input, if the result is zero
  JMP chk_no_inpt   ; else, jump back to checking if there is no input

multiply:           ; multiplies .multiplicand with .multiplier, stores it in .product
  LDA .multiplicand
  STA .product      ; store the multiplicand in .product, essentially doing the first addition
mult_loop:
  LDA .multiplier
  SUI 1             ; subtract 1 from multiplier
  JPZ mult_end      ; if result is zero, end multiply
  STA .multiplier
  LDA .product
  ADD .multiplicand
  STA .product
  JMP mult_loop     ; continue multiply loop
mult_end:
  LDA .product
  RET               ; return

divide:             ; divides .dividend with .divisor, stores the quotient in .quotient
  LDI 0
  STA .quotient     ; initialize quotient as 0
  LDI 1
  STA .pow2         ; initialize pow2 as 1
inc_b:              ; increase the .divisor by dividing by 2 as many times as possible
                    ; while keeping it below the dividend
  LDA .divisor      ; load divisor into A
  LSA               ; left shift (i.e. mult. by 2)
  JPC div_loop      ; if the left shift causes a carry, done increasing .divisor
  CMP .dividend     ; compare with dividend
  JPC div_loop      ; done increasing .divisor
  STA .divisor      ; store divisor*2 as new divisor
  LDA .pow2
  LSA               ; multiply pow2 by 2 as well
  STA .pow2         ; store it as new pow2
  JMP inc_b         ; go back to inc_b
div_loop:
  LDA .pow2
  CPI 0             ; compare pow2 to zero
  JPZ div_end       ; if zero, done divide
  LDA .dividend
  CMP .divisor      ; compare dividend to divisor
  JPC a_geq_b       ; if dividend >= divisor
  JMP continue      ; else
a_geq_b:
  LDA .quotient
  ADD .pow2         
  STA .quotient     ; add pow2 to the quotient and store it
  LDA .dividend
  SUB .divisor
  STA .dividend     ; subtract divisor from the dividend and store it
continue:
  LDA .divisor
  RSA
  STA .divisor      ; divide divisor by 2 and store
  LDA .pow2
  RSA
  STA .pow2         ; divide pow2 by 2 and store
  JMP div_loop
div_end:
  LDA .dividend
  STA .remainder
  LDA .quotient
  RET

handle_add_div:
  JPZ handle_add
  JMP handle_div

handle_add:
  LDA .result
  ADD .userval
  JMP end_calc

handle_sub:
  LDA .result
  SUB .userval
  JMP end_calc

handle_mul:
  LDA .userval
  ADI 0             ; add zero to it
  JPZ zero          ; if the input is zero, the result should be zero, handle that separately
  STA .multiplier 
  LDA .result
  STA .multiplicand
  JSR multiply      ; multiply
  JMP end_calc

handle_div:
  LDA .userval
  ADI 0
  JPZ disp_rem      ; can't divide by zero, display remainder instead
  STA .divisor
  LDA .result
  STA .dividend
  JSR divide        ; divide
  JMP end_calc

disp_rem:           ; load remainder from memory and display it
  LDA .remainder
  JMP end_calc

zero:               ; if multiplier or divisor is zero, output the result as 0
  STA .product      ; .quotient and .product have the same memory address
  JMP end_calc

end_calc:           ; common instructions after ending any calculation
  OUT               ; output result
  STA .result
  LDI 0
  STA .userval      ; set current input to 0
  JMP chk_no_inpt