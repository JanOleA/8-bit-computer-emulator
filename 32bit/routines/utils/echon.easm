;! name: ECHON
;! entry: echon
;! deps: newline, write_char
;! abi: os
;! bss: none
;! align: 4

; Prints exactly N characters from pointer .arg1, then newline.
; Inputs:
;   .arg1 = pointer to first character
;   .arg2 = if 1, skip newline. Else print it.
;   .num_digits = count (N)

echon:
  ; If .num_digits == 0, compute length until null (cap 80)
  LDA .num_digits
  CPI 0
  JNZ en_print

en_compute_len:
  LDI 0
  STA .num_digits
en_len_loop:
  ; read *(arg1 + num_digits)
  LDA .arg1
  ADD .num_digits
  PHA
  LAS
  CPI 0
  JPZ en_len_done
  ; inc length
  LDA .num_digits
  ADI 1
  STA .num_digits
  ; cap at 80
  LDA .num_digits
  CPI 80
  JPZ en_len_done
  JMP en_len_loop
en_len_done:
  ; fallthrough to printing

en_print:

lp:
  ; load *arg1 into .char
  LDA .arg1
  PHA
  LAS
  STA .char
  ; write char
  JSR @write_char
  ; advance arg1 and decrement N
  LDA .arg1
  ADI 1
  STA .arg1
  LDA .num_digits
  SUI 1
  STA .num_digits
  JNZ lp

end:
  LDA .arg2
  CPI 1
  JPZ return
  JSR @newline
return:
  RET